/*
 * Copyright (C) 2012 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

buildscript {
  repositories {
    add(new org.apache.ivy.plugins.resolver.URLResolver()) {
      name = 'GitHub'
      addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
    }
    mavenCentral()
  }

  dependencies {
    classpath 'bmuschko:gradle-izpack-plugin:0.2.1'
    classpath 'org.gradle.api.plugins:gradle-android-plugin:1.2.1'
  }

}

allprojects {
  if (project.hasProperty("buildTypeProp")) {
    ext.buildType = project.buildTypeProp
  } else {
    ext.buildType = "standard"
  }

  if (buildType == 'android') {
    ext.androidPlatformHome = getProperty("android.sdk.home") + "/platforms/" + getProperty("android.platform")
    ext.androidPlatformTools = getProperty("android.sdk.home") + "/platform-tools"
    ext.androidSdkJar = "$androidPlatformHome/android.jar"
  }

  ext.osgiVersion = '1.4.2'

  group = 'interactivespaces'
  version = osgiVersion
}

subprojects {
  if (name != 'interactivespaces_docs' && name != 'interactivespaces-controller-android'&& name != 'service') {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'

    if (name != 'interactivespaces-bootstrap-core') {
      apply plugin: 'osgi'
    }

    // Add in configuratations to the standard Java build
    configurations {
      // dependencies which are provided by the container.
      provided
    }

    sourceSets {
      main { 
           // Provided libraries should be on the compile classpath, though
           // not in the compile configuration.
      	   compileClasspath += configurations.provided 
      }
    }

    // Provided dependencies need to be available to eclipse.
    eclipse.classpath.plusConfigurations += configurations.provided

    sourceCompatibility = 1.6

    repositories {
      mavenLocal()
      maven {
        url 'http://robotbrains.hideho.org/nexus/content/groups/ros-public'
      }
      mavenCentral()
    }

    install.dependsOn build
  }
}

defaultTasks 'install'
